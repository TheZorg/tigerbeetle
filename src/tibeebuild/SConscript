import os.path


Import(['env', 'common'])

target = 'tibeebuild'

libs = [
    'boost_program_options',
    'boost_filesystem',
    'boost_system',
    common,
]

main_sources = [
    'main.cpp',
    'AbstractTracePlaybackListener.cpp',
    'AbstractCacheBuilder.cpp',
    'BuilderBeetle.cpp',
    'ProgressPublisher.cpp',
    'StateHistoryBuilder.cpp',
    'TraceDeck.cpp',
]

rpc_sources = [
    'BuilderJsonRpcMessageEncoder.cpp',
    'ProgressUpdateRpcNotification.cpp',
]

subs = [
    ('.', main_sources),
    ('rpc', rpc_sources),
]

sources = []
for base, files in subs:
    sources += [os.path.join(base, f) for f in files]

app_env = env.Clone()

app_env.Append(LIBS=libs)
app_env.ParseConfig('pkg-config --cflags --libs yajl')

app = app_env.Program(target=target, source=sources)

Return('app')
