import os.path


Import('env')

target = 'tibeecommon'

trace_sources = [
    'AbstractEventValue.cpp',
    'ArrayEventValue.cpp',
    'DictEventValue.cpp',
    'EnumEventValue.cpp',
    'Event.cpp',
    'EventInfos.cpp',
    'EventValueFactory.cpp',
    'FieldInfos.cpp',
    'FloatEventValue.cpp',
    'NullEventValue.cpp',
    'SintEventValue.cpp',
    'StringEventValue.cpp',
    'TraceInfos.cpp',
    'TraceSet.cpp',
    'TraceSetIterator.cpp',
    'UintEventValue.cpp',
]

state_sources = [
    'AbstractStateValue.cpp',
    'CurrentState.cpp',
    'StateHistorySink.cpp',
]

stateprov_sources = [
    'AbstractStateProvider.cpp',
    'AbstractStateProviderFile.cpp',
    'DynamicLibraryStateProvider.cpp',
    'PythonStateProvider.cpp',
]

mq_sources = [
    'AbstractMqSocket.cpp',
    'MqContext.cpp',
    'MqMessage.cpp',
]

rpc_sources = [
    'AbstractRpcMessage.cpp',
    'AbstractRpcRequest.cpp',
    'AbstractRpcResponse.cpp',
    'AbstractRpcNotification.cpp',
    'AbstractJsonRpcMessageEncoder.cpp',
    'AbstractJsonRpcMessageDecoder.cpp',
]

subs = [
    ('trace', trace_sources),
    ('state', state_sources),
    ('stateprov', stateprov_sources),
    ('mq', mq_sources),
    ('rpc', rpc_sources),
]

sources = []
for base, files in subs:
    sources += [os.path.join(base, f) for f in files]

lib_env = env.Clone()

# used by libbabeltrace
lib_env.ParseConfig('pkg-config --cflags uuid')
lib_env.ParseConfig('pkg-config --cflags glib-2.0')

lib_env.ParseConfig('pkg-config --cflags --libs yajl')
lib_env.ParseConfig('pkg-config --cflags --libs libzmq')

libs = [
    'delorean',
    'babeltrace',
    'babeltrace-ctf',
    'dl',
    'boost_regex',
    'boost_filesystem',
    'boost_system',
]

lib_env.Append(LIBS=libs)

lib = lib_env.SharedLibrary(target=target, source=sources)

Return('lib')
