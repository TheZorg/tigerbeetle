import os.path


Import('env')

target = 'tibeecommon'

trace_sources = [
    'AbstractEventValue.cpp',
    'ArrayEventValue.cpp',
    'DictEventValue.cpp',
    'EnumEventValue.cpp',
    'Event.cpp',
    'EventValueFactory.cpp',
    'FloatEventValue.cpp',
    'SintEventValue.cpp',
    'StringEventValue.cpp',
    'TraceSet.cpp',
    'TraceSetIterator.cpp',
    'UintEventValue.cpp',
    'UnknownEventValue.cpp',
]

state_sources = [
    'AbstractStateValue.cpp',
    'CurrentState.cpp',
    'StateHistorySink.cpp',
]

mq_sources = [
    'AbstractMqSocket.cpp',
    'MqContext.cpp',
    'MqMessage.cpp',
]

rpc_sources = [
    'AbstractRpcMessage.cpp',
    'AbstractRpcRequest.cpp',
    'AbstractRpcResponse.cpp',
    'AbstractRpcNotification.cpp',
    'AbstractJsonRpcMessageEncoder.cpp',
    'AbstractJsonRpcMessageDecoder.cpp',
]

subs = [
    ('trace', trace_sources),
    ('state', state_sources),
    ('mq', mq_sources),
    ('rpc', rpc_sources),
]

sources = []
for base, files in subs:
    sources += [os.path.join(base, f) for f in files]

lib_env = env.Clone()

lib_env.ParseConfig('pkg-config --cflags --libs yajl')
lib_env.ParseConfig('pkg-config --cflags --libs libzmq')
lib_env.ParseConfig('pkg-config --cflags uuid')
lib_env.Append(LIBS=['delorean', 'babeltrace', 'babeltrace-ctf'])

lib = lib_env.SharedLibrary(target=target, source=sources)

Return('lib')
